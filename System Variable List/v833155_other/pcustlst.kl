-------------------------------------------------------------------------------
--	  Provides User with 50 List Menus 1-25  GRS4 Press standards.
--									   26-50 Custom Applications for user.
--
--	  lst_menu[1,1] = Menu Title 		  [40 Characters MAX]
--	  lst_menu[1,2] = Menu Option 1 	  [40 Characters MAX]
--	  lst_menu[1,3] = Menu Option 2  	  [40 Characters MAX]
--	  lst_menu[1,4] = Menu Option 3  	  [40 Characters MAX]
--	  lst_menu[1,5] = Menu Option 4  	  [40 Characters MAX]
--	  lst_menu[1,6] = Menu Option 5  	  [40 Characters MAX]
--	  lst_menu[1,7] = Menu Option 6  	  [40 Characters MAX]
--	  lst_menu[1,8] = Menu Option 7  	  [40 Characters MAX]
--	  lst_menu[1,9] = Menu Option 8    	  [40 Characters MAX]
--
--    Example:
--	  lst_menu[1,1] = SPR Henrob Rivet Machine Fault  
--	  lst_menu[1,2] = Menu Option 1 	  
--	  lst_menu[1,3] = Menu Option 2  	  
--	  lst_menu[1,4] = Menu Option 3  	  
--	  lst_menu[1,5] = Menu Option 4  	  
--	  lst_menu[1,6] = Menu Option 5  	  
--	  lst_menu[1,7] = Menu Option 6  	  
--	  lst_menu[1,8] = Menu Option 7  	  
--	  lst_menu[1,9] = Menu Option 8    	  
--					 Enter Selection = ( Valid response is 1 thru 8)
--
-------------------------------------------------------------------------------
--    06-15-10   Steedlj Custom List Replacements, under same 
--                       conditions as all boxes to repost.  
--                       Cycle Start, TP reset, Panel Reset. 
--						 User has upto 8 list options to pick from.
--
--    10-15-13   jja R30iB Development
--    12-03-13   GJK Revised program for R30iB
--    05-14-13   GJK Added Dress Verification Guage and Resistance Check
--               Menus to menus 24 and 25.
--	  10-4-14    Steedlj Initial Press G3 / G4 Release
-------------------------------------------------------------------------------
PROGRAM pcustlst
-------------------------------------------------------------------------------

%RWACCESS
%NOPAUSESHFT
%NOLOCKGROUP
%COMMENT ='GRS4PCustLSTv4.0'  
%NOPAUSE = ERROR + TPENABLE + COMMAND
%NOABORT = ERROR + COMMAND

-- Softpart built-ins
%ENVIRONMENT SWGDEF
%ENVIRONMENT SGDEF1
%ENVIRONMENT proddef
%ENVIRONMENT SYSDEF
%ENVIRONMENT strng
%ENVIRONMENT REGOPE
%ENVIRONMENT UIF
%ENVIRONMENT ioblt
%ENVIRONMENT IOSETUP
%ENVIRONMENT MULTI
%ENVIRONMENT ATSTYDEF

%INCLUDE kliotyps
%INCLUDE gmevrs4io  

CONST

  ER_WARN       =  0
  ER_PAUSE      =  1
  ER_ABORT      =  2
  TPI_ENABLE    = 249 
  TPI_EDIT      = 145
  TPI_RESET     = 153  -- TP RESET
  CLR_SCRN		= 128  --clear the screen
  CRSR_HOME		= 137  --moves cursor to home position
  IGNOR_OFFLIN  = 128

VAR

  lst_menu      IN SHADOW FROM PCUSTLST : ARRAY[50,9] OF STRING[40]  
  lst_menunum   IN SHADOW FROM PCUSTLST : INTEGER
  lst_answer    IN SHADOW FROM PCUSTLST : INTEGER
  lst_nmchoice  IN SHADOW FROM PCUSTLST : INTEGER
  check_val     IN SHADOW FROM PCUSTLST : BOOLEAN
  prmpt_back    IN SHADOW FROM GMCUSTO  : BOOLEAN
  dnet64size    IN SHADOW FROM GMCUSTO  : BOOLEAN

  ans      IN SHADOW : INTEGER
  lst_flag IN SHADOW : BOOLEAN
  l_i     : INTEGER
  l_j     : INTEGER
  status  : INTEGER
  psve    : INTEGER
  real_value : REAL
  real_value2: REAL
  real_value3: REAL
  str_value  : STRING[10]
  str_value2 : STRING[10]
  str_value3 : STRING[10]
  gm_status  : INTEGER
  done       : BOOLEAN 
  cont_next IN CMOS FROM FFR_OPTN: BOOLEAN  --jja  R30iB  Was going to delete FFR_OPTN.KL but to many programs calling this variable
  l_ignore   : INTEGER

  --jja  R30iB Update02
  l_val,
  l_stat     : INTEGER    

  
%INCLUDE kliosop  
%INCLUDE etdcapsh
%INCLUDE klrdutil
%INCLUDE klrdread

-----------------------------------------------------------------------------
ROUTINE Menu_Screen 
-- DESCRIPTION: 
--   This routine is to define the GM USer Menu Screen 
-----------------------------------------------------------------------------
VAR
  l_status: INTEGER
BEGIN

    -- PCUSTLST                                1234567890123456789012345678901234567890
    SET_VAR(psve, 'PCUSTLST', 'LST_MENU[1,1]', 'MATERIAL HANDLING', gm_status)
    SET_VAR(psve, 'PCUSTLST', 'LST_MENU[1,2]', 'PART LOST FAULT ', gm_status)
    SET_VAR(psve, 'PCUSTLST', 'LST_MENU[1,3]', '  ', gm_status)
    SET_VAR(psve, 'PCUSTLST', 'LST_MENU[1,4]', '  ', gm_status)
    SET_VAR(psve, 'PCUSTLST', 'LST_MENU[1,5]', ' 1 = Retry', gm_status)
    SET_VAR(psve, 'PCUSTLST', 'LST_MENU[1,6]', ' 2 = Ignore', gm_status)
    SET_VAR(psve, 'PCUSTLST', 'LST_MENU[1,7]', ' 3 = Drop', gm_status)
    SET_VAR(psve, 'PCUSTLST', 'LST_MENU[1,8]', '  ', gm_status)
    SET_VAR(psve, 'PCUSTLST', 'LST_MENU[1,9]', ' Valid Response (1 or 2 or 3)', gm_status)

END Menu_Screen

-----------------------------------------------------------------------------
ROUTINE Post_Custlst 
-- DESCRIPTION: 
--   This routine Clears the user screen and Posts Custolst to it 
-----------------------------------------------------------------------------
VAR
  l_status: INTEGER
  
BEGIN

  SET_VAR(psve, '*system*', '$PRMPDSPON', TRUE, gm_status)
  prmpt_back = TRUE

     WRITE TPDISPLAY (CHR(CLR_SCRN),CHR(CRSR_HOME))
     
  --Custom List Prompts
      WRITE (lst_menu[lst_menunum,1],cr)
      WRITE (lst_menu[lst_menunum,2],cr)
      WRITE (lst_menu[lst_menunum,3],cr)
      WRITE (lst_menu[lst_menunum,4],cr)
      WRITE (lst_menu[lst_menunum,5],cr)
      WRITE (lst_menu[lst_menunum,6],cr)
      WRITE (lst_menu[lst_menunum,7],cr)
      WRITE (lst_menu[lst_menunum,8],cr)
      WRITE (lst_menu[lst_menunum,9],cr)
      WRITE ('Enter selection = ' )

  FORCE_SPMENU(tp_panel,SPI_TPUSER,1)

END Post_Custlst      
   
-----------------------------------------------------------------------------
-- pcustlst
-----------------------------------------------------------------------------

BEGIN --custolst

  --done = FALSE
  lst_flag = FALSE

  -- Reset Menu Title, Line1, Line 2, Response
  FOR l_i = 1 to 50 DO
    FOR l_j = 1 to 9 DO 
      IF UNINIT (lst_menu[l_i,l_j]) THEN
        lst_menu[l_i,l_j] = ''  
      ENDIF
    ENDFOR
  ENDFOR
  
  IF UNINIT(lst_answer) THEN
    lst_answer = 40
  ENDIF 
  IF UNINIT(check_val) THEN
    check_val = TRUE
  ENDIF
  IF UNINIT(ans) THEN
    ans = 0
  ENDIF
 
  GET_TPE_PRM(1,1, lst_menunum, real_value, str_value, status)     -- Get the MENU Number
  GET_TPE_PRM(2,1, lst_answer, real_value2, str_value2, status)    -- Get the R[X] to write the answer to.
  GET_TPE_PRM(3,1, lst_nmchoice, real_value3, str_value3, status)  -- Get the number of choices to check against.

  -- Checks to make sure your using valid arguements.
  -- turn off the parameter checking.
  IF check_val = TRUE THEN  
    IF ((lst_menunum > 50) OR (lst_nmchoice = 0) OR (lst_nmchoice > 5) OR (lst_answer = 0)) THEN  --GJK changed lst_menunum > 10 to lst_menunum > 50 051414
      POST_ERR(ER_TYPEER,'Invalid params AR[1]AR[2]AR[3]',0,2 )
      ABORT
    ELSE
    ENDIF
  ENDIF

  Menu_Screen

  IF status = 0 THEN
    FORCE_SPMENU(tp_panel,SPI_TPUSER,1)
 
     done = FALSE     

     REPEAT
     
       lst_flag = TRUE
       Post_Custlst
       READ (ans)

     UNTIL ( (ans >= 1) AND (ans <= lst_nmchoice) )
 
     IF ( (ans >= 1) AND (ans <= lst_nmchoice) ) THEN
       -- Set R[X] for answer
       SET_INT_REG(lst_answer, ans, status)
         lst_flag = FALSE
         SET_VAR(psve, '*system*', '$PRMPDSPON', FALSE, gm_status)
         prmpt_back = FALSE
     ENDIF

    FORCE_SPMENU(tp_panel,SPI_TPTCH,1)

  ENDIF
END pcustlst