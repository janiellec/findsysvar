---------------------------------------------------------------------
--
--   This program defines and enables the Condition handler used 
--   to repost the GM GRS4 Custom menu programs. (custook.pc, 
--   custoyn.pc, and custolst.pc)
--
---------------------------------------------------------------------
-- gjk   02-DEC-2013   Creation Date
---------------------------------------------------------------------
PROGRAM menu_pst
---------------------------------------------------------------------
%SYSTEM
%RWACCESS
%NOBUSYLAMP
%NOPAUSESHFT
%NOLOCKGROUP
%COMMENT='CHsCustMnus v4.0'
%NOPAUSE = ERROR + TPENABLE + COMMAND
%NOABORT = ERROR + COMMAND

-- Softpart built-ins
%ENVIRONMENT SYSDEF
%ENVIRONMENT STRNG
%ENVIRONMENT FLBT
%ENVIRONMENT REGOPE
%ENVIRONMENT MULTI
%ENVIRONMENT IOSETUP
%ENVIRONMENT swgdef

%INCLUDE kliotyps

CONST
  CH_RPOST      = 675  
  TPI_ENABLE    = 249 


VAR
	l_time			: integer
	stop_mon		: boolean
	cond_trig		: boolean
	count				: integer
  gm_status  	: INTEGER
  prmpt_back	IN SHADOW FROM GMCUSTO  : BOOLEAN
  dnet64size  IN SHADOW FROM GMCUSTO  : BOOLEAN
  ok_flag			IN SHADOW FROM CUSTOOK : BOOLEAN
  yn_flag			IN SHADOW FROM CUSTOYN : BOOLEAN
  lst_flag		IN SHADOW FROM CUSTOLST : BOOLEAN

ROUTINE Post_Custlst from CUSTOLST
ROUTINE Post_Custok from CUSTOOK
ROUTINE Post_Custyn from CUSTOYN

-----------------------------------------------------------
-- Initializes All Variables
-----------------------------------------------------------

ROUTINE InitVars

BEGIN
  IF UNINIT(l_time) THEN l_time = 500 ; ENDIF
  IF UNINIT(stop_mon) THEN stop_mon = FALSE ; ENDIF
  IF UNINIT(cond_trig) THEN cond_trig = FALSE ; ENDIF
  IF UNINIT(count) THEN count = 0 ; ENDIF
  IF UNINIT(ok_flag) THEN ok_flag = FALSE ; ENDIF
  IF UNINIT(yn_flag) THEN yn_flag = FALSE ; ENDIF
  IF UNINIT(lst_flag) THEN lst_flag = FALSE ; ENDIF
END InitVars
-----------------------------------------------------------
-- Initializes Condition handlers
-----------------------------------------------------------

ROUTINE InitConds

BEGIN
--Define and enable repost condition
     	  CONDITION[CH_RPOST]: WITH $SCAN_TIME = 150
          WHEN (NOT TPIN[tpi_enable]) AND ok_flag = TRUE AND DIN[6]+ DO
           	cond_trig = TRUE
           	Post_Custok
     	      ENABLE CONDITION[CH_RPOST]

          WHEN (NOT TPIN[tpi_enable]) AND ok_flag = TRUE AND TPIN[153]+ DO
           	cond_trig = TRUE
           	Post_Custok
     	      ENABLE CONDITION[CH_RPOST]

          WHEN (NOT TPIN[tpi_enable]) AND ok_flag = TRUE AND OPIN[1]+ DO
           	cond_trig = TRUE
           	Post_Custok
     	      ENABLE CONDITION[CH_RPOST]

          WHEN (NOT TPIN[tpi_enable]) AND yn_flag = TRUE AND DIN[6]+ DO
           	cond_trig = TRUE
           	Post_Custyn
     	      ENABLE CONDITION[CH_RPOST]

          WHEN (NOT TPIN[tpi_enable]) AND yn_flag = TRUE AND TPIN[153]+ DO
           	cond_trig = TRUE
           	Post_Custyn
     	      ENABLE CONDITION[CH_RPOST]

          WHEN (NOT TPIN[tpi_enable]) AND yn_flag = TRUE AND OPIN[1]+ DO
           	cond_trig = TRUE
           	Post_Custyn
     	      ENABLE CONDITION[CH_RPOST]

          WHEN (NOT TPIN[tpi_enable]) AND lst_flag = TRUE AND DIN[6]+ DO
           	cond_trig = TRUE
           	Post_Custlst
     	      ENABLE CONDITION[CH_RPOST]

          WHEN (NOT TPIN[tpi_enable]) AND lst_flag = TRUE AND TPIN[153]+ DO
           	cond_trig = TRUE
           	Post_Custlst
     	      ENABLE CONDITION[CH_RPOST]

          WHEN (NOT TPIN[tpi_enable]) AND lst_flag = TRUE AND OPIN[1]+ DO
           	cond_trig = TRUE
           	Post_Custlst
     	      ENABLE CONDITION[CH_RPOST]
        ENDCONDITION

          ENABLE CONDITION[CH_RPOST]

END InitConds
--------------------------------------------------------------------
--------------------------------------------------------------------
--------------------------------------------------------------------
BEGIN
  InitVars
  InitConds
  REPEAT
  DELAY (l_time)
  IF (cond_trig = TRUE) THEN
    cond_trig = FALSE
    count = count + 1
  ENDIF
  UNTIL (stop_mon)
  stop_mon =FALSE
  
END menu_pst
