--    This material is the joint property of Fanuc Robotics Corporation  and
--    FANUC  LTD  Japan,  and  must  be  returned  to  either Fanuc Robotics
--    Corporation or FANUC LTD Japan immediately upon request.  This  material
--    and   the  information  illustrated  or  contained  herein  may  not  be
--    reproduced, copied, used, or transmitted in whole or in part in any  way
--    without the prior written consent of both Fanuc Robotics and FANUC.
--
--             All Rights Reserved
--             Copyright (C)   1999
--             Fanuc Robotics Corporation
--
--             Karel is a registered trademark of
--             Fanuc Robotics Corporation
--    +
--    Program:  gminstal - RS4 Specific Program
--
--    Description:
--
--    Runs during software installation
--
--    Language: KAREL
--
--    Source File:  sw102292.kl    9-AUG-99   8:14AM
--
--    Author:
--            Fanuc Robotics Corporation
--            3900 W. Hamlin Rd.
--            Rochester Hills, Michigan    48309
--
--    Modification history:
--
--    2016-03-17   marchaka Added call to routine to setup PMC internal IO assignment.
--                          Removed loading of sggmdress.  Wizard will handle this if required.
--                          Modified safety setup to run gmsafeio instead.
--                          Modified hints screen for G4.
--    2016-05-31   marchaka Added position register array increase to 200
--    2016-06-14   marchaka Removed macro table and user alarms  default settings.
--			    They will be handled in FD and in the wizard.
--    2016-11-14   marchaka Setup number of tip dress schedules based on installed software options
--    2016-12-5    marchaka Setup number of tip dressers to 2.
--    2017-01-30   marchaka Add initgmwiwizvar routine call to initialize wizard variables.  Remove
--                          srvo_optn and srvotd_optn routine calls, they are in initgmwizvar.
--    2017-02-13   marchaka Add "\" for file loading.  The KCL CHDIR command doesn't work during install.
--
--    2017-10-02   kosaskirr updated teh Rev to reflect auto exit fix
--    2019-08-02   marchaka Remove Telnet.  FSAC read only.
--    2019-08-22   marchaka Add some file_apbck settings during software installation for debug purposes
--    2019-10-17   marchaka Temporarily remove fsac settings per GM's request, need other settings before these can be applied
--    2020-09-15   marchaka Call routine to load FMD files from custom folder on full load media.
------------------------------------------------------------------
PROGRAM gminstal
------------------------------------------------------------------

%COMMENT = 'GMRS4 site setup'
%NOLOCKGROUP
%INVISIBLE
%RWACCESS
%NOPAUSE = ERROR + COMMAND + TPENABLE
%NOABORT = ERROR + COMMAND


%ENVIRONMENT ATCLDEF
%ENVIRONMENT IOSETUP
%ENVIRONMENT celldef
%ENVIRONMENT DNSVDEF
%ENVIRONMENT MNMCDEF
%ENVIRONMENT MULTI
%ENVIRONMENT SLDEF
%ENVIRONMENT SWGDEF
%ENVIRONMENT SYSDEF
%ENVIRONMENT tpthrdef
%ENVIRONMENT sgdef1
%ENVIRONMENT REGOPE
%ENVIRONMENT proddef
%ENVIRONMENT FDEV
%ENVIRONMENT TPE
%ENVIRONMENT SYCRDEF

%INCLUDE klevkmsk
%INCLUDE kltpctrl
%INCLUDE kliotyps

%INCLUDE KLIOSOP
CONST
  Revision = '2020-09-15'
  ER_WARN = 0

VAR
  i            : INTEGER
  pop_index    : INTEGER
  prog_index   : INTEGER
  screen       : STRING[4]
  status       : INTEGER
  entry        : INTEGER
  g_psve       : INTEGER
  gm_status    : INTEGER   -- error gm_status
  l_i          : INTEGER
  l_stat       : INTEGER
  l_nowt       : BOOLEAN
  l_fnm        : STRING[12]
  custo_name: STRING[18]
  sw_version: STRING[18]
  sw_rev_num: STRING[18]
  comment1:   STRING[18]
  comment2:   STRING[18]
  comment3:   STRING[18]

  sgunchng     IN SHADOW FROM GMCUSTO  : BOOLEAN   -- Servo gun tool changer option loaded
  srvo_td_opt  IN SHADOW FROM GMCUSTO  : BOOLEAN --servo tip dresser option loaded on the robot
  g_servog_opt IN SHADOW FROM GMVARS : BOOLEAN   -- Servo gun option loaded
  arc_optn IN SHADOW FROM GMVARS: BOOLEAN --arctool loaded on the robot

  CurAppLoc : STRING[32] -- used for setting fileappbck variable


%INCLUDE klrdutil
%INCLUDE gmrdutil  --GM utility program

ROUTINE init_setup  FROM gmsetvar
ROUTINE set_std_var FROM gmsetvar
ROUTINE setpmcintio FROM gmpmc
ROUTINE GMPTLOAD FROM gmpwrtn
ROUTINE initgmwizvar FROM gmwizard
ROUTINE arc_loaded FROM gmmain1
ROUTINE fmd_load FROM gmcellpg
----------------------------------------------------------------------------------------
BEGIN

  arc_loaded --check if ArcTool is installed

  init_setup

  initgmwizvar --initialize wizard variables

  set_std_var  --set necessary Karel variables

  -- Set default arrays that are not *system* and cannot be set in fd
  Set_I_PVar('*NUMREG*','$MAXREGNUM',999)  --set registers to 999
  Set_I_PVar('*STRREG*','$MAXSREGNUM',99) --set string registers to 99
  Set_I_PVar('*POSREG*','$MAXPREGNUM',200) --set position registers to 200
  Set_I_sVar('$scr.$maxnumufram',20)  --set user frames to 20
  Set_I_sVar('$scr.$maxnumutool',20)  --set tool frames to 20

  --Add some file_apbck settings during install
  -- Setup so the atcustom is saved on Backup
  IF ChkIfBckedup('atcustom') THEN -- check if already in setup
    POST_ERR(38000, 'ATCUSTOM is already setup in FILE APP BACKUP', gm_status, 0)
  ELSE -- Not already in file_appbck
    CurAppLoc = SetBackUpLoc -- Call routine to return available variable location
    IF CurAppLoc <> 'NONE' THEN
      Set_S_sVar(CurAppLoc + '.$file_name','atcustom.vr')
      Set_S_sVar(CurAppLoc + '.$prog_name','atcustom')
      Set_I_sVar(CurAppLoc + '.$func_code',0)
      Set_I_sVar(CurAppLoc + '.$modifier',0)
      Set_S_sVar(CurAppLoc + '.$comment','Customization GRS4')
    ELSE
      POST_ERR(38000, 'No FILE BACKUP Opening for ATCUSTOM to be set to', gm_status, 0)
    ENDIF
  ENDIF

  -- Setup so Custom data file gets saved on backup
  IF ChkIfBckedup('gmcusto') THEN -- check if already in setup
    POST_ERR(38000, 'gmcusto is already setup in FILE APP BACKUP', gm_status, 0)
  ELSE -- Not already in file_appbck
    CurAppLoc = SetBackUpLoc -- Call routine to return available variable location
    IF CurAppLoc <> 'NONE' THEN
      Set_S_sVar(CurAppLoc + '.$file_name','gmcusto.vr')
      Set_S_sVar(CurAppLoc + '.$prog_name','gmcusto')
      Set_I_sVar(CurAppLoc + '.$func_code',0)
      Set_I_sVar(CurAppLoc + '.$modifier',0)
      Set_S_sVar(CurAppLoc + '.$comment','Custom File')
    ELSE
      POST_ERR(38000, 'No FILE BACKUP Opening for gmcusto to be set to', gm_status, 0)
    ENDIF
  ENDIF

  -- Setup so all wizard configuration data files get saved on backup
  IF ChkIfBckedup('gmcfg*') THEN -- check if already in setup
    POST_ERR(38000, 'gmcfg* is already setup in FILE APP BACKUP', gm_status, 0)
  ELSE -- Not already in file_appbck
    CurAppLoc = SetBackUpLoc -- Call routine to return available variable location
    IF CurAppLoc <> 'NONE' THEN
      Set_S_sVar(CurAppLoc + '.$file_name','gmcfg*.vr')
      Set_S_sVar(CurAppLoc + '.$prog_name','gmcfg*')
      Set_I_sVar(CurAppLoc + '.$func_code',0)
      Set_I_sVar(CurAppLoc + '.$modifier',0)
      Set_S_sVar(CurAppLoc + '.$comment','Wizard Configs')
    ELSE
      POST_ERR(38000, 'No FILE BACKUP Opening for gmcfg* to be set to', gm_status, 0)
    ENDIF
  ENDIF

  -- Set custom menu setting for pounce data
  Set_S_sVar('$CUSTOMMENU[22].$TITLE', 'Pounce Data')
  Set_S_sVar('$CUSTOMMENU[22].$PROG_NAME', 'GMPNCDTA')
  Set_I_sVar('$CUSTOMMENU[22].$OPTION', 31)

  IF (g_servog_opt = TRUE) THEN --servo gun option is loaded on the robot
    IF (srvo_td_opt = TRUE) THEN --servo tip dress option is loaded on the robot
      Set_I_sVar('$sgtdcfg.$num_sched',4) --number of dresser schedules to display
                                          --should be 8 if there is a servo gun change, but robot doesn't
                                          --think gun change is installed yet when this program is run
      Set_I_sVar('$sgtdcfg.$num_dresrs',2) --number of tip dressers
                                           --should be 4 if there is a servo gun change, but robot doesn't
                                           --think gun change is installed yet when this program is run
    ENDIF --servo tip dress option is loaded on the robot
  ENDIF --servo gun option is loaded on the robot

  -- Menu Protection at Cold Start Spot Config Screen
  SPRUNCMD ('frclrpr', gm_status)
  SPRUNCMD ('COPY FRS:\gmsplus.as  FRSU:\splus.as',gm_status)
    IF (gm_status <> 0) THEN
      POST_ERR(38000, 'Failed to copy splus.as to FRSU', 0, 0)
    ENDIF

  --Load gmatcstm
  CLEAR('atcustom', status)
  LOAD('FRS:\gmatcstm.pc',0,gm_status)

  -- Set the port comment for SOP LED#1 and #2
  -- but only for fra params loaded.
  IF feature('R650') THEN
     SET_PORT_CMT(IO_SOPOUT, SOPO_USER1, 'Proc cmplete', status)
     SET_PORT_CMT(IO_SOPOUT, SOPO_USER2, 'At home', status)
  ENDIF

  setpmcintio --Map PMC internal IO assignments

  CALL_PROG('GMSAFEIO',prog_index) --Setup GM safety settings

  --Load powertrain files
  GMPTLOAD

  --Load FMD files
  fmd_load

  Set_B_PVar('atcustom','Prgs2RunCell[2].NeedToRun', TRUE)
  Set_B_PVar('atcustom','Prgs2RunCell[2].NeedForProd', TRUE)
  Set_S_PVar('atcustom','Prgs2RunCell[2].Prog_Name', 'GMASPD')

  --Fanuc Server Access Control set to read only
  --Temporarily removed from p08 release per GM's request, need other settings before these can be applied
  --Set_I_sVar('$fsac_enable', 1)
  --Set_I_sVar('$fsac_def_lv', 0)

  SPRUNCfile('FRS:\telnet.ld4', 'FR2:\telnet.ld4')
  SPRUNCMD('frclrpr', gm_status)  --unprotects FRS
  IF (gm_status <> 0) THEN
    POST_ERR(38000, 'Failed to run FLRCLRPR', gm_status, 0)
  ENDIF
  DELETE_FILE('FRS:\telnet.ld4',false, gm_status)
  IF (gm_status <> 0) THEN
    POST_ERR(38000, 'Failed to delete TELNET', gm_status, 0)
  ENDIF
  SPRUNCMD('frsetpr', gm_status)  --protects FRS
  IF (gm_status <> 0) THEN
    POST_ERR(38000, 'Failed to run FRSETPR', gm_status, 0)
  ENDIF

  $application[8] = 'GM Global 4 Rev00'

END gminstal





