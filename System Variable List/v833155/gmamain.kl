-- -----------------------------------------------------------------------
--
--   MODULE:         GMAMAIN.KL
--   TITLE:          GMAMAIN PROGRAM
--
--
--   WRITTEN BY:     Fanuc
--   REVISION DATE:  2016/10/08
--
--   LANGUAGE:       KAREL 8.33 R30iB
--   CONTROL GROUP:  BODY SHOP EXECUTION GROUP
--   STATUS:
--
--
--   ORDER NUMBER:   GMGBL4
--   PROJECT NAME:   GM Global Center
--
--
--         FANUC Robotics retains rights in any and all Software
--         contained in the material attached hereto and said
--         Software may not be copied or reproduced without the
--         written permission of FANUC Robotics.
--
--         All Software, (C) Copyright FANUC Robotics Corporation, 2016
--         DESCRIPTION: GM ArcTool robot configuration program.  Executes 
--                      robot configuration programs.
--                      
--                      
--
--         HISTORY OF CHANGES:
--
--         REVISION   DATE       BY               COMMENTS
--         --------   ---------  ---------------  --------------------
--              v4.1  2018-09-11 marchaka         Per GM's request for v833p06, DCS will only be setup during wizard execution
--                                                if the user answered yes to DCS setup in the wizard.  DCS setup still occurs
--                                                during software installation no matter what.
--              v4.1  2018-12-03 marchaka         Add a config_comp variable check to determine when cold start setup can occur.
--              v4.2  2020-10-23 schoensm	  Added Mig-Weld Wizard during V8.33P10                                  
--
--
--
--
--------------------------------------------------------------------------
PROGRAM gmamain

%COMMENT = 'GM ARC Main V4.2'

%NOLOCKGROUP
%INVISIBLE
%RWACCESS
%NOPAUSE = ERROR + COMMAND + TPENABLE

%INCLUDE KLEVCCDF  -- Required for the TPERROR write command for the contants

%INCLUDE gmcfgcel  -- GM wizard variables for workcell variables that are common across all shops
%INCLUDE gmcfgerr  -- Required for the configuration for GM Error counting and logging

CONST

Version = '2020-10-26'
%INCLUDE gmcnstnt -- all GM constants for the wizard

VAR
  GM_SaftyDone IN SHADOW FROM GMSAFEIO: BOOLEAN -- Used for tracking if the Saftey has been setup
  coldcfg_done IN CMOS:  BOOLEAN --robot cold start config is done 
  gm_status:  INTEGER
  entry:  INTEGER

  collab_rbt    IN SHADOW FROM GMVARS :BOOLEAN  --Collaborative robot arm 


%INCLUDE klrdutil  --Routine declaration for core util
%INCLUDE gmrdutil  --GM utilities

ROUTINE load_arc_pmc    FROM gmpmc
ROUTINE set_reg     FROM gmcellrg
ROUTINE set_cell_io FROM gmcellio
ROUTINE set_cellvar  FROM gmcellvr
ROUTINE set_cellpg  FROM gmcellpg
ROUTINE set_zdt FROM gmcellvr
ROUTINE set_arc_cfg FROM gmarctl
ROUTINE arc_app_bit FROM gmarcvr --set arc application bits
ROUTINE set_colb_io  FROM gmcolbio  --collaborative robot I/O setup
ROUTINE set_colb_var FROM gmcolbvr  --collaborative robot VAR setup
ROUTINE set_colb_pg  FROM gmcolbpg  --collaborative robot PRG setup
-------------------------------------------------------------------
ROUTINE a_collab_arm
--check to see if collaborative robot arm is being used
------------------------------------------------------------------
VAR
cr35:  BOOLEAN
cr7:   BOOLEAN
cr4:   BOOLEAN
cr7L:  BOOLEAN
cr15:  BOOLEAN
cr14L:  BOOLEAN

BEGIN

--initialize to false
collab_rbt = FALSE 
cr35       = FALSE
cr7        = FALSE
cr4        = FALSE
cr7L       = FALSE
cr15       = FALSE
cr14L      = FALSE

IF Chk_Feature('H768') THEN --CR-35iA
  cr35 = TRUE
ENDIF

IF Chk_Feature('H766') THEN --CR-7iA
  cr7 = TRUE
ENDIF

IF Chk_Feature('H765') THEN --CR-4iA
  cr4 = TRUE
ENDIF

IF Chk_Feature('H767') THEN --CR-7LiA/L
  cr7L = TRUE
ENDIF

IF Chk_Feature('H787') THEN --CR-15iA
  cr15 = TRUE
ENDIF

IF Chk_Feature('H788') THEN --CR-14iA/L
  cr14L = TRUE
ENDIF

IF ((cr35 = TRUE) OR (cr7 = TRUE) OR (cr4 = TRUE) OR (cr7L = TRUE) OR (cr15 = TRUE) OR (cr14L = TRUE)) THEN
  collab_rbt = TRUE
ENDIF

END a_collab_arm
-----------------------------------------------------------------------------
ROUTINE set_arc_cold
-- PURPOSE: 
--  This routine will perform the setup that is needed on power up after
--  the custo is ran (from set_std_ctrl).  Call on cold start power up.
--
--
-- INPUT:
--            -
-- OUTPUT:
--            -
-- CALLED BY:
--
--
-----------------------------------------------------------------------------
BEGIN

  IF (UNINIT(wiz_exec)) THEN
    wiz_exec = FALSE
  ENDIF

  GET_VAR(entry, 'gmwizarc', 'config_comp', coldcfg_done, gm_status) --see if cold start setup is already done
    IF (gm_status <> 0) THEN
      POST_ERR(38000, '[gmwizarc].config_comp FAILED', gm_status, 0) --
    ENDIF

    IF UNINIT(coldcfg_done) THEN
      coldcfg_done = FALSE
    ENDIF

  IF ((wiz_exec) AND (coldcfg_done = FALSE)) THEN

    do_kcl_Cmnd('CHDIR FRS:\') --reset FRS to root directory
    do_kcl_Cmnd('CHDIR FR:\') --reset FR to root directory

    arc_app_bit --set Arctool application bits
    set_zdt --sets ZDT variable settings

    wiz_exec = FALSE
    Reset_Cell = FALSE

    SET_VAR(entry, 'gmwizarc', 'config_comp', TRUE, gm_status)
      IF (gm_status <> 0) THEN
        POST_ERR(38000, '[gmwizarc].config_comp FAILED', gm_status, 0) --
      ENDIF 

  ENDIF

END set_arc_cold
-----------------------------------------------------------------------------
ROUTINE set_arc_ctrl
-- PURPOSE: 
--   This routine will perform all setup possible at controlled start for all applications
--
-- INPUT:
--            -
-- OUTPUT:
--            -
-- CALLED BY:
--
--
-----------------------------------------------------------------------------
BEGIN

  IF (UNINIT(collab_rbt)) THEN
    a_collab_arm
  ENDIF

  do_kcl_Cmnd('CHDIR FRS:\') --reset FRS to root directory
  do_kcl_Cmnd('CHDIR FR:\') --reset FR to root directory

  IF (Chk_loaded('gmpmc')) THEN
    load_arc_pmc          --load ArcTool pmc
  ELSE
    ErrsInCell.ErrorsPrgrms = ErrsInCell.ErrorsPrgrms+1
  ENDIF

  IF UNINIT(set_safeio) THEN 
    set_safeio = wizans_yes          
  ENDIF

  IF (set_safeio = wizans_yes) THEN	--Setup DCS only if user answered yes during wizard
    IF NOT (CallThisProg('gmsafeio')) THEN
      ErrsInCell.ErrorsPrgrms = ErrsInCell.ErrorsPrgrms+1
    ENDIF
  ENDIF

  IF (Chk_loaded('gmcellrg')) THEN
      set_reg						 --sets registers
  ELSE
    ErrsInCell.ErrorsRgstrs = ErrsInCell.ErrorsRgstrs+1
  ENDIF

  IF (Chk_loaded('gmcellio')) THEN
    set_cell_io					 --sets cell I/O and flags
  ELSE
    ErrsInCell.ErrorsIOCom = ErrsInCell.ErrorsIOCom+1
  ENDIF

  IF (Chk_loaded('gmcellvr')) THEN
    set_cellvar					 --sets cell variables
  ELSE
    ErrsInCell.ErrorsVarbls = ErrsInCell.ErrorsVarbls+1
  ENDIF

  IF (Chk_loaded('gmcellpg')) THEN
    set_cellpg
  ELSE
    ErrsInCell.ErrorsPrgrms = ErrsInCell.ErrorsPrgrms+1
  ENDIF

  IF (collab_rbt = TRUE) THEN --collaborative robot
    IF (Chk_loaded('gmcolbio')) THEN --collaborative robot I/O setup 
      set_colb_io
    ELSE
      ErrsInShop.ErrorsIOCom = ErrsInShop.ErrorsIOCom+1
    ENDIF
    IF (Chk_loaded('gmcolbvr')) THEN --collaboratve robot variable setup
      set_colb_var
    ELSE
      ErrsInShop.ErrorsVarbls = ErrsInShop.ErrorsVarbls+1
    ENDIF
    IF (Chk_loaded('gmcolbpg')) THEN --collaboratve robot program setup
      set_colb_pg
    ELSE
      ErrsInCell.ErrorsPrgrms = ErrsInCell.ErrorsPrgrms+1
    ENDIF
  ENDIF --collaborative robot

  --Setup ArcTool Shop
  IF (Chk_loaded('gmarctl')) THEN
    set_arc_cfg  --runs ArcTool setup
  ELSE
    ErrsInShop.ErrorsPrgrms = ErrsInShop.ErrorsPrgrms+1
  ENDIF
  
END set_arc_ctrl
-----------------------------------------------------------------------------
BEGIN

END gmamain
