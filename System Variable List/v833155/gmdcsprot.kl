-- -----------------------------------------------------------------------
--
--   MODULE:         GMDCSPROT.KL
--   TITLE:          GMDCSPROT PROGRAM
--
--
--   WRITTEN BY:     FANUC
--   REVISION DATE:  2019/03/05
--
--   LANGUAGE:       KAREL 8.33 R30iB
--   CONTROL GROUP:  BODY SHOP EXECUTION GROUP
--   STATUS:
--
--
--   ORDER NUMBER:   GMGBL4
--   PROJECT NAME:   GM Global Center
--
--
--         FANUC Robotics retains rights in any and all Software
--         contained in the material attached hereto and said
--         Software may not be copied or reproduced without the
--         written permission of FANUC Robotics.
--
--         All Software, (C) Copyright FANUC Robotics Corporation, 2016
--         DESCRIPTION: This program is used to determine if the robot is using the new
--                      v833p06 and later write protected DCS I/O setup. 
--                      If it is, then do nothing.
--                      If it is not the new setup than the write protection
--                      of the DCS I/O connect needs to be cleared and turned off.
--                      This program is executed during an auto update.
--                      
--
--         HISTORY OF CHANGES:
--
--         REVISION   DATE       BY               COMMENTS
--         --------   ---------  ---------------  --------------------
--           4.0      2019/04/12 marchaka	  Initial release.
--           4.1      2019/09/25 marchaka         Use GetSysVar utility to read the DCS variables.
--
--
--
--
--------------------------------------------------------------------------
PROGRAM gmdcsprot

%COMMENT = 'DCS Prot V4.1'
%NOLOCKGROUP
--%INVISIBLE                                  
%NOPAUSE = ERROR + COMMAND + TPENABLE

%INCLUDE kliotyps -- used for identifying the I/O Type constants
%INCLUDE KLEVCCDF -- Used for the TPPERROR builtin

CONST

-- Used by the Safety I/O COnnect routine for variable setup
NO_Logic = 0
OR_Logic = 4
AND_Logic = 8
NOT_R1Type = 1
NOT_R2Type = 2

DCS_SPI =  1   --Safe Position Input
DCS_SPO =  2   --Safe Position Output
DCS_SSI =  3   --Safe System Inputs
DCS_SSO =  4   --Safe System Output
DCS_SIR =  5   --Safe Internal Relay
DCS_CPC =  6   --Cartesian Position Check Status
DCS_CSC =  7   --Cartesian Speed Check Status
DCS_JPC =  8   --Joint Position Check Status
DCS_JSC =  9   --Joint Speed Check Status
DCS_CSI = 10   --CIP Saftey Input
DCS_CSO = 11   --CIP Saftey Outputs
DCS_CCL = 12   --Configuration Change Latch
DCS_CCR = 13   --Configuration Change Reset
DCS_RPI = 14   --ROBOT POWER INPUT
DCS_RPO = 15   --ROBOT POWER OUTPUT
DCS_FSI = 16   --FL-NET SAFETY INPUT
DCS_FSO = 17   --FL-net Safety Output
--SLO= 19 -- ???
Version = '2019-09-25'

%INCLUDE gmcnstnt  -- Constant file for GM

VAR

  i           : INTEGER --used for FOR loops
  entry       : INTEGER
  gm_status   : INTEGER -- used for the built-ins return value for evaluation

  Current_Ver  IN SHADOW :STRING[32] -- Set equal to Version constant in initialization routine

  gmsafeioprot IN SHADOW FROM ATCUSTOM: BOOLEAN

  dcs_var :STRING[32]-- used for indirecting a DCS var adding an index to the default var name
  SVrRtrndInt FROM GMUTILITY : INTEGER -- Used for returned integer in the GET_SVAR routine

%INCLUDE klrdutil
%INCLUDE gmrdutil -- has all the builtin routines i.e. Int2str, was GMUTILTY

ROUTINE set_one_sfio(loc, operation, l_typ, l_idx, r1_typ, r1_idx, r2_typ, r2_idx: INTEGER) FROM gmsafeio
ROUTINE set_onepsfio(loc, operation, l_typ, l_idx, r1_typ, r1_idx, r2_typ, r2_idx: INTEGER) FROM gmsafeio

-------------------------------------------------------------------------------------------------------
-----------------------------------------------------------------------------
ROUTINE dcsio_prot
-- PURPOSE:  Determine if write protection is required or not.
--           If it is not, turn off write protection and clear the write 
--           protected safety I/O settings.
--
--
--
--
-- INPUT:
--            -
-- OUTPUT:
--            -
-- CALLED BY:
--
--
--
-----------------------------------------------------------------------------
VAR
finish: integer
io_conn_chk: integer
io_conn_item: integer


BEGIN

IF Chk_Feature('J568') THEN --DCS Safe I/O Connect

  --Check to see if write protection and hiding of portion of the DCS I/O connect
  --map is required.
  --Reading of I/O connect array item 45 should suffice.
  --v833p06 and later DCS I/O connect maps have write protection for item 45, so it should
  --not ever be changed.
  --If it is detected that it is pre-v833p06 I/O connect map, then we need to turn
  --off write protection and disabling the hiding of the menu.  If it is the v833p06
  --version and later, then nothing is required.

  --SSO[1] = CSI[1] OR SSI[11]
  io_conn_item = 45 --DCS I/O Connect Item 
  io_conn_chk = 0 --initialize

  dcs_var = '$DCSS_IOC[' + int2str(io_conn_item) + '].'

  -- Use GET_VAR builtin in GMUTILITY, it's INTEGER var so pass 2. (1=StringVar.2=IntegerVar,3=RealVar)
  IF GetSysVar(dcs_var +'$OPERATION',2) THEN
    IF (SVrRtrndInt = OR_Logic) THEN  -- SVrRtrndInt is declared in GMUTILITY & set to Var value when succesful read
      io_conn_chk = 1
    ENDIF
  ENDIF

  IF GetSysVar(dcs_var +'$L_TYP',2) THEN
    IF (SVrRtrndInt = DCS_SSO) THEN
      io_conn_chk = io_conn_chk + 1
    ENDIF
  ENDIF

  IF GetSysVar(dcs_var +'$L_IDX',2) THEN
    IF (SVrRtrndInt = 1) THEN
      io_conn_chk = io_conn_chk + 1
    ENDIF
  ENDIF

  IF GetSysVar(dcs_var +'$R1_TYP',2) THEN
    IF (SVrRtrndInt = DCS_CSI) THEN
      io_conn_chk = io_conn_chk + 1
    ENDIF
  ENDIF

  IF GetSysVar(dcs_var +'$R1_IDX',2) THEN
    IF (SVrRtrndInt = 1) THEN
      io_conn_chk = io_conn_chk + 1
    ENDIF
  ENDIF

  IF GetSysVar(dcs_var +'$R2_TYP',2) THEN
    IF (SVrRtrndInt = DCS_SSI) THEN
      io_conn_chk = io_conn_chk + 1
    ENDIF
  ENDIF

  IF GetSysVar(dcs_var +'$R2_IDX',2) THEN
    IF (SVrRtrndInt = 11) THEN
      io_conn_chk = io_conn_chk + 1
    ENDIF
  ENDIF

  IF (io_conn_chk <> 7) THEN --I/O connect is pre v833p06 settings, turn off protection

    --set end of clearing of VARs
    finish = 64

    FOR i = 45 TO finish DO   --clear DCS I/O protection variables
      set_onepsfio(i, 0, 0, 0, 0, 0, 0, 0)
    ENDFOR 

    --Disable write protection and hiding of last 20 lines in I/O connect menu
    Set_I_sVar('$dcs_piocnum', 0)  --write protects the last x amount of lines in the I/O connect menu 
    Set_B_sVar('$dcs_cfg.$pioc_disp', true)  --hides the last x amount of lines in the I/O connect menu 
    Set_B_PVar('atcustom', 'gmsafeioprot', FALSE)

  ENDIF

ENDIF --J568 DCS Safe I/O Connect
  
END dcsio_prot
-----------------------------------------------------------------------------
BEGIN

IF Chk_Feature('J568') THEN --DCS Safe I/O Connect

  --set version
  Current_Ver = VERSION

  --Check to see if DCS I/O write protection is needed, if not, turn it off
  dcsio_prot

ENDIF --J568 DCS Safe I/O Connect

END gmdcsprot
